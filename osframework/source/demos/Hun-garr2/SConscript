# -*- coding: utf-8 -*-
# -*- python -*-
# Author: Luo Jinghua

import os
import sys
import game
import configs
from intltoolize import intltoolize, installLocale, stripFonts

srcdir = Dir('.').srcnode().abspath

Import('gameenv')

env = gameenv.Clone()
language = 'all' #env['language']
domain = 'hungarr2'
env.PrependUnique(CPPPATH = ['#', '#SexyAppFramework'],
                  CPPDEFINES = [('SEXY_LANG', configs.QuoteStr(''))],
                  LIBPATH = [os.path.join ('$top_builddir', 'SexyAppFramework'),
                             os.path.join ('$top_builddir', 'PakLib'),
                             os.path.join ('$top_builddir', 'ImageLib'),
                             os.path.join ('$top_builddir', 'audiere', 'lib')],
                  LIBS = ['SexyAppFramework'])
srcs = ['Board.cpp', 'GameApp.cpp', 'GameOverEffect.cpp',
        'LevelupEffect.cpp', 'OptionsDialog.cpp', 'TitleScreen.cpp',
        'Res.cpp']
if env['TARGET_PLATFORM'] == 'android':
    srcs += ['AndroidMain.cpp']
    configs.EnablePackage(env, 'android')
    env.AppendUnique(LIBS = ['log'])
    hungarr = env.SharedLibrary('GameMain', srcs)
else:
    srcs += ['main.cpp']
    hungarr = env.Program('hungarr2', srcs)

### install game hungarr
game_name = 'Hun-garr2'
destdir = game.GetGameDistDir(env, game_name)

### install targets for hungarr
targets = []

### data
files = []
#files.append(os.path.join(srcdir, 'fonts'))
files.append(os.path.join(srcdir, 'images'))
files.append(os.path.join(srcdir, 'music'))
files.append(os.path.join(srcdir, 'sounds'))

### properties
files.append(os.path.join(srcdir, 'properties'))

### intltoolize
intltoolize(env, '.', 'po', domain)

targets += installLocale(env, '.',
                         'po', domain, language,
                         os.path.join(destdir, 'locale'))

### fonts
targets += env.Install(os.path.join(destdir, 'fonts'),
                       Glob(os.path.join(srcdir, 'fonts', '*.gif')) +
                       Glob(os.path.join(srcdir, 'fonts', '*.png')) +
                       Glob(os.path.join(srcdir, 'fonts', '*.txt')))
targets += stripFonts(env, '.',
                      Glob(os.path.join(srcdir, 'fonts', '*.ttf')),
                      'po', os.path.join(destdir, 'fonts'), language)

targets = game.InstallGame(env, game_name, hungarr, destdir, files, targets)

### build apk
if env['TARGET_PLATFORM'] == 'android':
    targets += game.APK(env, '', srcdir, destdir, targets,
                        'android', 'android-build')

env.Alias('hungarr2_install', targets)


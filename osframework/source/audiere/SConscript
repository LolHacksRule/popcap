import os
import game
import configs

from SCons.Defaults import *

Import (['env', 'gameenv'])

# don't build audiere and its dependences if it's not enabled.
if not 'AUDIERESOUND' in env['DRIVERS']:
    Return ()

srcdir = Dir('.').srcnode().abspath

ogg_name = 'libogg'
ogg_version = '1.1.3'
ogg_suffix = '.tar.gz'
ogg_tar = ogg_name + '-' + ogg_version + ogg_suffix
ogg_dir = ogg_name + '-' + ogg_version

vorbis_name = 'libvorbis'
vorbis_version = '1.2.1rc1'
vorbis_suffix = '.tar.bz2'
vorbis_tar = vorbis_name + '-' + vorbis_version + vorbis_suffix
vorbis_dir = vorbis_name + '-' + vorbis_version

audiere_name = 'audiere'
audiere_version = '1.9.4.1'
audiere_suffix = '.tar.gz'
audiere_tar = audiere_name + '-' + audiere_version + audiere_suffix
audiere_dir = audiere_name + '-' + audiere_version

tarballs = [ogg_tar, vorbis_tar, audiere_tar]
for tarball in tarballs:
    fpath = os.path.join (srcdir, tarball)
    if not os.path.exists(fpath):
        print fpath, "doesn't exist."
        Return ()

common_env = env.Clone()
#common_env.AppendUnique (CPPPATH = ['include'], LIBPATH = ['lib'])

### ogg
env = common_env.Clone()
ogg_untar = env.Untar (os.path.join (srcdir, ogg_tar), ['*src*', '*include*'], ['*Makefile*'])
env.SourceCode (ogg_dir, ogg_untar)

headers = []
headers += env.Command (os.path.join (ogg_dir, 'include', 'ogg',
                                      'config_types.h'),
                        'libogg-config_types.h',
                        Copy ("$TARGET", "$SOURCE"))

sources = map (lambda f: os.path.join(ogg_dir, 'src', f),
              ['framing.c', 'bitwise.c'])
Depends(sources, headers)

lib_dir = os.path.join('lib')
header_dir = os.path.join('include', 'ogg')

ogg_headers = env.Install (header_dir,
                           map(lambda f:os.path.join(ogg_dir, 'include',
                                                     'ogg', f),
                               ['os_types.h', 'ogg.h', 'config_types.h']))

env.PrependUnique(CPPPATH = [os.path.join (ogg_dir, 'include')])

ogg_lib = env.ConvenienceLibrary ('ogg', sources)
Depends (sources, ogg_headers)

ogg = env.Install (lib_dir, ogg_lib)

env.Clean (ogg, [ogg_dir])

### vorbis & vorbisfile
env = common_env.Clone ()

vorbis_untar = env.Untar (os.path.join (srcdir, vorbis_tar))
env.SourceCode (vorbis_dir, vorbis_untar)

sources = map (lambda f: os.path.join(vorbis_dir, 'lib', f),
              ['mdct.c', 'smallft.c', 'block.c', 'envelope.c', 'window.c', 'lsp.c',
               'lpc.c', 'analysis.c', 'synthesis.c', 'psy.c', 'info.c',
               'floor1.c', 'floor0.c', 'res0.c', 'mapping0.c', 'registry.c',
               'codebook.c', 'sharedbook.c', 'lookup.c', 'bitrate.c'])

env.AppendUnique (CPPPATH = [os.path.join (vorbis_dir, 'include')])

vorbis_lib = env.ConvenienceLibrary ('vorbis', sources)

sources = map (lambda f: os.path.join(vorbis_dir, 'lib', f),
              ['vorbisfile.c'])
vorbisfile_lib = env.ConvenienceLibrary ('vorbisfile', sources)

header_dir = os.path.join('include', 'vorbis')
Mkdir(header_dir)

vorbis_headers = env.Install(header_dir,
                             map(lambda f:os.path.join(vorbis_dir,
                                                       'include', 'vorbis', f),
                                 ['codec.h', 'vorbisenc.h', 'vorbisfile.h']))
vorbis = env.Install (lib_dir, vorbis_lib)
vorbis += env.Install (lib_dir, vorbisfile_lib)
Depends (vorbis, vorbis_headers)

env.Clean (vorbis, [vorbis_dir])

### audiere
env = common_env.Clone ()

audiere_untar = env.Untar (os.path.join (srcdir, audiere_tar))
env.SourceCode (audiere_dir, audiere_untar)

srcs = [
  'basic_source.cpp',
  'debug.cpp',
  'device.cpp',
  'device_mixer.cpp',
  'device_null.cpp',
  'dumb_resample.cpp',
  'file_ansi.cpp',
  'input.cpp',
  'input_aiff.cpp',
  'input_mp3.cpp',
  'input_wav.cpp',
  'input_ogg.cpp',
  'loop_point_source.cpp',
  'memory_file.cpp',
  'noise.cpp',
  'resampler.cpp',
  'sample_buffer.cpp',
  'sound.cpp',
  'sound_effect.cpp',
  'square_wave.cpp',
  'tone.cpp',
  'utility.cpp',
  'version.cpp'
]

srcs += [os.path.join ('mpaudec', 'mpaudec.c'),
         os.path.join ('mpaudec', 'bits.c')]

env.AppendUnique (CPPDEFINES = ['NO_FLAC', 'NO_DUMB', 'NO_SPEEX'])
env.AppendUnique (CPPDEFINES = ['AUDIERE_EXPORTS'])
env.AppendUnique (LIBS = ['vorbisfile', 'vorbis', 'ogg'])

if 'win32' in env['TARGET_OS']:
    env.AppendUnique (CPPDEFINES = ['WIN32_LEAN_AND_MEAN', 'NOMINMAX',
                                    'HAVE_DSOUND'],
                      LIBS = ['winmm', 'dsound', 'ole32', 'rpcrt4',
                             'user32'])
    srcs += ['device_mm.cpp', 'midi_mci.cpp', 'cd_win32.cpp',
             'timer_win32.cpp', 'threads_win32.cpp']
    srcs += [ 'device_ds_buffer.cpp', 'device_ds_stream.cpp',
              'device_ds.cpp', 'dxguid.cpp' ]
elif 'linux' in env['TARGET_OS']:
    env.AppendUnique(CPPDEFINES = ['HAVE_ALSA'],
                     LIBS = ['asound'])
    srcs += ['device_alsa.cpp']
    srcs += ['midi_null.cpp', 'cd_null.cpp',
             'timer_posix.cpp', 'threads_posix.cpp']
elif 'darwin' in env['TARGET_OS']:
    env.AppendUnique (CPPDEFINES = ['HAVE_CORE_AUDIO'],
                      LINKFLAGS = [('-framework', 'CoreAudio'),
                                   ('-framework', 'Cocoa'),
                                   ('-framework', 'AudioUnit'),
                                   ('-framework', 'AudioToolbox')],
                      CCFLAGS = [('-framework', 'CoreAudio')])
    srcs += ['device_coreaudio.cpp', 'midi_null.cpp', 'cd_null.cpp',
             'timer_posix.cpp', 'threads_posix.cpp']
else:
    srcs += ['midi_null.cpp', 'cd_null.cpp',
             'timer_posix.cpp', 'threads_posix.cpp']

sources = map(lambda f:os.path.join (audiere_dir, 'src', f), srcs)

if env['static_audiere']:
    audiere_lib = env.Library ('audiere', sources)
else:
    audiere_lib = env.SharedLibrary ('audiere', sources)

### install audiere header & library
header_dir = 'include'

audiere = env.Install (header_dir,
                       map(lambda f:os.path.join (audiere_dir,
                                                  'src', f),
                           ['audiere.h']))
audiere += env.Install (lib_dir, audiere_lib)
env.Alias ('audiere_install', audiere)

### sdk
header_dir = os.path.join('$sdkdir', 'include')
lib_dir = os.path.join('$sdkdir', 'lib')

audiere_sdk = env.Install (header_dir,
                           map(lambda f:os.path.join (audiere_dir,
                                                      'src', f),
                               ['audiere.h']))

audiere_sdk += env.Install (lib_dir, [audiere_lib, vorbisfile_lib,
                                      vorbis_lib, ogg_lib])

env.Alias ('audiere_sdk_install', audiere_sdk)
env.Alias ('sdk_install', audiere_sdk)

### extra clean targets
env.Clean (audiere, [audiere_dir])
env.Clean (audiere, ['include', 'lib'])

### a test program
env.Program('audiere_play',
            os.path.join(audiere_dir, 'examples', 'simple', 'simple.cpp'),
            CPPPATH = [os.path.join(audiere_dir, 'srcs')] + env['CPPPATH'],
            LIBPATH = ['.'] + env['LIBPATH'],
            LIBS = ['audiere'] + env['LIBS'])

### setup package info
depends = []
objects = []
if env['static_audiere']:
    depends += ['vorbisfile', 'vorbis', 'ogg']
else:
    objects = audiere_lib
configs.SetPackageInfo([env, gameenv], 'audiere',
                       LIBS = ['audiere'] + depends,
                       CPPPATH = [os.path.join('$top_builddir', 'audiere', 'include')],
                       LIBPATH = [os.path.join('$top_builddir', 'audiere', 'lib')],
                       OBJECTS = objects, TARGETS = audiere)
